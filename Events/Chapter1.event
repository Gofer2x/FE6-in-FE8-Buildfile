//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

//Tile Changes
//SouthVillageDestroyed 0x0
//NorthVillageDestroyed 0x1
//SouthVillageVisited 0x2
//NorthVillageVisited 0x3

//Flags/EventIDs
//BossQuoteFlag 0x1
//BossDefeatFlag 0x2
//SeizeFlag 0x3
//SecondBGMFlag 0x4
// 0x5
//RoutFlag 0x6

EventPointerTable(EVENTS_Ch1,ThisChapter)

ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventEnemy(0x0,CursesReinforcementsAlready,1)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Village(0x0,SouthVillageVisit,12,19)
House(0x0,DoomHouseVisit,12,15)
House(0x0,TerrainHouseVisit,9,16)
House(0x0,MoneyHouseVisit,9,19)
Seize(5,4)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

//just here so it looks nice in febuilder tbh
Units:
UNIT Roy RoyLord 0x0 Level(1,Ally,0) [2,18] 0x0 0x0 0x0 0x0 [Rapier, Vulnerary] DoNothingAI
UNIT Marcus Paladin Roy Level(1,Ally,0) [1,17] 0x0 0x0 0x0 0x0 [IronSword, SilverLance] DoNothingAI
UNIT Lance Cavalier Roy Level(1,Ally,0) [4,18] 0x0 0x0 0x0 0x0 [IronSword, Javelin, Vulnerary] DoNothingAI
UNIT Alen Cavalier Roy Level(1,Ally,0) [3,17] 0x0 0x0 0x0 0x0 [IronSword, IronLance] DoNothingAI
UNIT Bors Knight Roy Level(1,Ally,0) [3,19] 0x0 0x0 0x0 0x0 [IronLance, Vulnerary] DoNothingAI
UNIT Wolt Archer Roy Level(1,Ally,0) [2,16] 0x0 0x0 0x0 0x0 [IronBow, Vulnerary] DoNothingAI
UNIT

Bandits1:
UNIT GenericBandit Archer Damas Level(1,Enemy,0) [1,0] 0x0 0x0 0x1 REDA_1_2 [IronBow] TwoMovesAI
UNIT Damas Fighter 0x0 Level(1,Enemy,0) [5,0] 0x0 0x0 0x1 REDA_5_6 [SteelAxe] BossAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [2,6] 0x0 0x0 0x1 REDA_1_3 [IronAxe] TwoMovesAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [3,10] 0x0 0x0 0x1 REDA_4_5 [IronAxe] InRangeAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [12,10] 0x0 0x0 0x1 REDA_6_5 [IronAxe] InRangeAI
UNIT

BanditsBrigand:
UNIT GenericBandit Brigand Damas Level(1,Enemy,0) [7,4] 0x0 0x0 0x1 REDA_7_7 [IronAxe] BrigandAI
UNIT

Bandits2:
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [8,4] 0x0 0x0 0x1 REDA_8_9 [IronAxe] InRangeAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [9,4] 0x0 0x0 0x1 REDA_9_9 [IronAxe] InRangeAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [7,2] 0x0 0x0 0x1 REDA_11_7 [IronAxe] InRangeAI
UNIT GenericBandit Archer Damas Level(1,Enemy,0) [7,3] 0x0 0x0 0x1 REDA_10_10 [IronBow] AggressiveAI
UNIT

Bandits3:
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [12,7] 0x0 0x0 0x1 REDA_7_15 [IronAxe] AggressiveAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [12,8] 0x0 0x0 0x1 REDA_8_14 [IronAxe] AggressiveAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [3,8] 0x0 0x0 0x1 REDA_12_13 [IronAxe] AggressiveAI
UNIT GenericBandit Fighter Damas Level(1,Enemy,0) [4,8] 0x0 0x0 0x1 REDA_8_18 [IronAxe] AggressiveAI
UNIT

LanceRunner:
UNIT Lance Cavalier Roy Level(1,Ally,0) [10,9] 0x0 0x0 0x1 REDA_2_17 [IronSword, Javelin, Vulnerary] DoNothingAI
UNIT

TheBoys1:
UNIT Roy RoyLord Roy Level(1,Ally,0) [1,20] 0x0 0x0 0x1 REDA_2_18 [Rapier, Vulnerary] DoNothingAI
UNIT Marcus Paladin Roy Level(1,Ally,0) [2,20] 0x0 0x0 0x1 REDA_1_17 [IronSword, SilverLance] DoNothingAI
UNIT

TheBoys2:
UNIT Alen Cavalier Roy Level(1,Ally,0) [0,20] 0x0 0x0 0x1 REDA_3_17 [IronSword, IronLance] DoNothingAI
UNIT Bors Knight Roy Level(1,Ally,0) [1,20] 0x0 0x0 0x1 REDA_3_19 [IronLance, Vulnerary] DoNothingAI
UNIT Wolt Archer Roy Level(1,Ally,0) [2,20] 0x0 0x0 0x1 REDA_2_16 [IronBow, Vulnerary] DoNothingAI
UNIT


ALIGN 4

BeginningScene:
CMOF
MUSC MUS_AnUnexpectedCaller
LOAD1 0x1 Bandits1
ENUN
FlashCursor(Damas, 60)
Text(BG_Outside, Ch1_Start1)
CAM1 [7,8]
LOAD1 0x1 BanditsBrigand
ENUN
FlashCursor(7,7,60)
Text(BG_Outside, Ch1_StartBrigandVillage)
MoveByCoords(0x0, 7, 7, 4, 9)
ENUN
SOUN SND_VillageDestruction
TILECHANGE 0x1
FlashCursor(5,4,60)
Text(BG_Castle1, Ch1_Start2)
STAL 16
CAM1 [8,12]
CMOF
STAL 16
LOAD1 0x1 Bandits2
ENUN
CAM1 [8,17]
LOAD1 0x1 Bandits3
ENUN
STAL 16
LOAD1 0x1 TheBoys1
ENUN
LOAD1 0x1 TheBoys2
ENUN
LOAD1 0x1 LanceRunner
ENUN
Text(BG_Outside, Ch1_Start3)
MOVE 0x0 Lance [4,18]
EndEvent

CursesReinforcementsAlready:
MUSC MUS_DeerOfThePlains
CAM1[5,3]
ENUN
MOVE 0x0 Damas [5, 4]
ENUN
STAL 16
Text(BG_Outside, Ch1_Start4)
EndEvent

SouthVillageVisit:
VillageEventMoney(Ch1_SouthVillage,BG_Village,5000)

DoomHouseVisit:
HouseEvent(Ch1_DoomHouse,BG_House)

TerrainHouseVisit:
HouseEvent(Ch1_TerrainHouse,BG_House)

MoneyHouseVisit:
HouseEvent(Ch1_MoneyHouse,BG_House)

EndingScene:
MUSC MUS_Triumph
SetBackground(BG_Castle1)
TextNoBGNoREMA(Ch1_End1)
CHECK_ALIVE Bors
BEQ 1 s0 sC
TextNoBGNoREMA(Ch1_End2LilinaBors)
LABEL 1
TextNoBGNoREMA(Ch1_End3)
REMA
STAL 64
MoveToChapter(CHAPTER_Ch2)
ENDA

MESSAGE Events end at offset CURRENTOFFSET