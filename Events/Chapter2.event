//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

//Tile Changes
//VillageDestroyed 0x0
//VillageVisited 0x1

//Flags/EventIDs
//#define BossQuoteFlag 0x1
//#define BossDefeatFlag 0x2
//#define SeizeFlag 0x3
//#define SecondBGMFlag 0x4
// 0x5
//#define RoutFlag 0x6
#define Ch2_RoyDieckTalkFlag 0x7

EventPointerTable(EVENTS_Ch2,ThisChapter)

ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,MercsShowUp,2)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEventBothWays(Ch2_RoyDieckTalkFlag,RoyDieckTalk,Roy,Dieck)
END_MAIN

LocationBasedEvents:
Village(0x0,VillageVisit,3,4)
Vendor(VendorList,7,9)
Armory(ArmoryList,6,1)
House(0x0,FliersHouseVisit,6,7)
House(0x0,WeaponTriangleHouseVisit,0,4)
House(0x0,FortsHouseVisit,5,7)
Seize(16,3)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

JustRuud:
UNIT Ruud Knight 0x0 Level(5,Enemy,0) [16,3] 0x0 0x0 0x1 REDA_16_4 [Javelin] BossAI
UNIT

JustMelady:
UNIT Melady WyvernRiderF 0x0 Level(5,Enemy,0) [16,3] 0x0 0x0 0x0 0x0 [SteelLance, Javelin] DoNothingAI
UNIT

GateSoldier:
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [16,3] 0x0 0x0 0x0 0x0 [IronLance] AggressiveAI
UNIT

EnemiesRight:
UNIT GenericBern Fighter Ruud Level(2,Enemy,1) [17,13] 0x0 0x0 0x1 REDA_15_6 [IronAxe] InRangeAI //ghost of bern 0F,06 standby lol
UNIT GenericBern Archer Ruud Level(2,Enemy,1) [18,14] 0x0 0x0 0x1 REDA_17_4 [IronBow] InRangeAI //ghost of bern 11,04 standby lol
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [16,15] 0x0 0x0 0x0 REDA_18_6 [Javelin] AggressiveAI
UNIT GenericBern Fighter Ruud Level(1,Enemy,1) [17,12] 0x0 0x0 0x1 REDA_17_11 [IronAxe] TwoMovesAI
UNIT GenericBern Fighter Ruud Level(2,Enemy,1) [18,12] 0x0 0x0 0x1 REDA_18_10 [IronAxe] InRangeAI
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [18,12] 0x0 0x0 0x1 REDA_17_9 [Javelin] InRangeAI
UNIT GenericBern Soldier Ruud Level(2,Enemy,1) [18,12] 0x0 0x0 0x1 REDA_18_11 [IronLance] TwoMovesAI
UNIT GenericBern Fighter Ruud Level(1,Enemy,1) [18,13] 0x0 0x0 0x1 REDA_18_9 [IronAxe] AggressiveAI
UNIT GenericBern Soldier Ruud Level(1,Enemy,1) [18,15] 0x0 0x0 0x1 REDA_18_8 [Javelin] AggressiveAI
UNIT GenericBern Archer Ruud Level(1,Enemy,1) [16,12] 0x0 0x0 0x1 REDA_16_7 [IronBow] TwoMovesAI
UNIT GenericBern Fighter Ruud Level(3,Enemy,1) [16,12] 0x0 0x0 0x1 REDA_17_9 [SteelAxe] InRangeAI
UNIT GenericBern Mercenary Ruud Level(3,Enemy,1) [17,9] 0x0 0x0 0x1 REDA_17_7 [IronBlade] InRangeAI
UNIT

Units:
UNIT Roy RoyLord Roy Level(1,Ally,1) [2,10] 0x0 0x0 0x0 0x0 [IronSword, Rapier] DoNothingAI
UNIT Marcus Paladin Roy Level(1,Ally,1) [2,8] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary, Vulnerary] DoNothingAI
UNIT Alen Cavalier Roy Level(3,Ally,1) [3,9] 0x0 0x0 0x0 0x0 [IronSword, IronLance] DoNothingAI
UNIT Lance Cavalier Roy Level(2,Ally,1) [3,11] 0x0 0x0 0x0 0x0 [IronSword, Javelin, Vulnerary] DoNothingAI
UNIT Bors Knight Roy Level(2,Ally,1) [1,11] 0x0 0x0 0x0 0x0 [IronLance] DoNothingAI
UNIT Wolt Archer Roy Level(1,Ally,1) [0,10] 0x0 0x0 0x0 0x0 [IronBow] DoNothingAI
UNIT

JustMerlinus:
UNIT Merlinus Transporter Roy Level(1,Ally,0) [2,9] 0x0 0x0 0x1 REDA_1_9 [0x0] DoNothingAI
UNIT

JustElen:
UNIT Elen Cleric Roy Level(2,Ally,0) [4,14] 0x0 0x0 0x0 0x0 [Heal,Vulnerary] DoNothingAI
UNIT

ElenChaser:
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [7,17] 0x0 0x0 0x1 REDA_3_14 [IronLance] InRangeAI
UNIT

ElenChasers2:
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [8,18] 0x0 0x0 0x1 REDA_5_16 [IronLance] InRangeAI
UNIT GenericBern Soldier Ruud Level(3,Enemy,1) [8,18] 0x0 0x0 0x1 REDA_1_16 [IronLance] InRangeAI
UNIT

ElenChasers3:
UNIT GenericBern Fighter Ruud Level(1,Enemy,1) [11,18] 0x0 0x0 0x1 REDA_8_17 [IronAxe, HandAxe] AggressiveAI
UNIT GenericBern Fighter Ruud Level(1,Enemy,1) [11,18] 0x0 0x0 0x1 REDA_6_18 [IronAxe] AggressiveAI
UNIT GenericBern Fighter Ruud Level(2,Enemy,1) [11,18] 0x0 0x0 0x1 REDA_4_18 [HandAxe] InRangeAI
UNIT

WardAndLot:
UNIT Ward Fighter Dieck Level(2,Ally,0) [15,18] 0x0 0x0 0x1 REDA_12_16 [IronAxe, Hammer, Vulnerary] DoNothingAI
UNIT Lot Fighter Dieck Level(3,Ally,0) [15,18] 0x0 0x0 0x1 REDA_12_18 [IronAxe, SteelAxe, HandAxe] DoNothingAI
UNIT

DieckAndShanna:
UNIT Dieck Mercenary 0x0 Level(5,Ally,0) [15,18] 0x0 0x0 0x1 REDA_13_17 [IronBlade, IronSword] DoNothingAI
UNIT Shanna PegasusKnight Dieck Level(1,Ally,0) [16,18] 0x0 0x0 0x1 REDA_14_17 [SlimLance, Javelin, Vulnerary] DoNothingAI
UNIT

ALIGN 4

BeginningScene:
CAM1 [18,0]
CMOF
LOAD1 0x1 JustRuud
ENUN
LOAD1 0x1 JustMelady
ENUN
Text(Ch2_Start1)
MOVE 0x0 Ruud [17,4]
ENUN
MOVE 0x0 Melady [13,65535]
ENUN
DISA Melady
STAL 16
MOVE 0x0 Ruud [16,4]
ENUN
STAL 16
LOAD1 0x1 GateSoldier
MUSC MUS_DeerOfThePlains
Text(Ch2_Start2)
STAL 16
MoveByCoords(0x0,16,3,17,11)
ENUN
MOVE 0x0 Ruud [16,3]
ENUN
CMOF
LOAD1 0x1 EnemiesRight
ENUN
MUSCMID 0x7FFF
MUSC MUS_WhenTheRushComes
LOAD3 0x0 Units
ENUN
LOAD1 0x1 JustMerlinus
ENUN
STAL 16
CAM1 Roy
STAL 16
MOVE 0x0 Merlinus [2,9]
ENUN
Text(Ch2_Start3)
LOAD1 0x1 JustElen
ENUN
STAL 16
//move elen 3x up, ENUN, then 1x up and 1x left and ENUN.. i'll just do a normal move and not manual lol
MOVE 0x0 Elen [4,11]
ENUN
MOVE 0x0 Elen [3,10]
ENUN
Text(Ch2_Start4)
MUSC MUS_AnUnexpectedCaller
LOAD1 0x1 ElenChaser
ENUN
STAL 16
CAM1 [0,14]
Text(Ch2_Start5)
LOAD1 0x1 ElenChasers2
ENUN
LOAD1 0x1 ElenChasers3
ENUN
CAM1 Roy
Text(Ch2_Start6)
MOVE 0x0 Merlinus [1,9]
ENUN
MOVE 0x0 Elen [4,10]
ENUN
NoFade
ENDA

MercsShowUp:
STAL 32
MUSS MUS_JoinUs
STAL 32
LOAD1 0x1 WardAndLot
LOAD1 0x1 DieckAndShanna
Text(Ch2_MercsAppear1)
CMOF
MOVE 0x0 Shanna [0,13]
ENUN
MOVE 0x0 Ward [13,16]
ENUN
MOVE 0x0 Lot [13,18]
ENUN
Text(Ch2_MercsAppear2)
MOVE 0x0 Shanna [12,17]
ENUN
Text(Ch2_MercsAppear3)
MOVE 0x0 Ward [12,16]
MOVE 0x0 Lot [12,18]
MOVE 0x0 Shanna [14,17]
ENUN
MURE 0x0
NoFade
ENDA

RoyDieckTalk:
ConvoEvent(Ch2_RoyDieckTalk)

VillageVisit:
VillageEventItem(Ch2_ArmorslayerVillage,BG_Village,Armorslayer)

FliersHouseVisit:
HouseEvent(Ch2_FliersHouse,BG_House)

FortsHouseVisit:
HouseEvent(Ch2_FortsHouse,BG_House)

WeaponTriangleHouseVisit:
HouseEvent(Ch2_WeaponTriangleHouse,BG_House)



EndingScene:
MUSC MUS_Triumph
SetBackground(BG_Castle1)
TextNoBGNoREMA(Ch2_End1)
CHECK_ALIVE Elen
BEQ 1 s0 sC
TextNoBGNoREMA(Ch2_End2IfElen)
LABEL 1
TextNoBGNoREMA(Ch2_End3)
CHECK_ALIVE Elen
BEQ 2 s0 sC
TextNoBGNoREMA(Ch2_End4IfElen)
LABEL 2
TextNoBGNoREMA(Ch2_End5)
REMA
STAL 64
MNC2 CHAPTER_Ch3
ENDA

ArmoryList:
SHLI IronSword IronLance Javelin IronBow

VendorList:
SHLI Vulnerary

MESSAGE Events end at offset CURRENTOFFSET

